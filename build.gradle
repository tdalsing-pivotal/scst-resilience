plugins {
    id "java"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group = "com.vmware"
    version = "0.0.1-SNAPSHOT"

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }
    
    dependencyManagement {
        dependencies {
            dependency "org.springframework.boot:spring-boot-starter-webflux:2.6.6"
            dependency "org.springframework.cloud:spring-cloud-stream-binder-kafka:3.2.2"
            dependency "org.springframework.boot:spring-boot-starter-actuator:2.6.6"

            dependency "org.springframework.boot:spring-boot-starter-test:2.6.6"

            dependency "io.github.resilience4j:resilience4j-reactor:1.7.1"
            dependency "io.github.resilience4j:resilience4j-spring-cloud2:1.7.1"
            dependency "io.github.resilience4j:resilience4j-spring-boot2:1.7.1"

            dependency "org.apache.commons:commons-lang3:3.12.0"
            dependency "ch.qos.logback:logback-classic:1.2.3"
            dependency "io.micrometer:micrometer-core:1.8.4"
            dependency "io.micrometer:micrometer-registry-graphite:1.8.4"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.22"
        annotationProcessor "org.projectlombok:lombok:1.18.22"

        testCompileOnly "org.projectlombok:lombok:1.18.22"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.22"

        testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.1"

        testImplementation "org.mockito:mockito-core:3.9.0"
        testImplementation "org.assertj:assertj-core:3.19.0"
    }

    test {
        useJUnitPlatform {
            includeTags 'unit'
            excludeTags 'integration'
        }
    }

    task integrationTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
            excludeTags 'unit'
        }
    }
}
